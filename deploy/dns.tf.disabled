resource "google_dns_managed_zone" "zone" {
  name        = "terraform-bot-zone"
  dns_name    = "${var.domain_name}."
  description = "DNS zone for Terraform Bot"
}

resource "google_cloud_run_domain_mapping" "frontend" {
  location = var.region
  name     = var.domain_name

  metadata {
    namespace = var.project_id
  }

  spec {
    route_name = google_cloud_run_v2_service.frontend.name
  }
}

resource "google_dns_record_set" "run_records" {
  for_each = {
    for rr in google_cloud_run_domain_mapping.frontend.status[0].resource_records : "${rr.type}_${rr.name}" => rr
  }

  name         = each.value.name == "" ? "${var.domain_name}." : "${each.value.name}.${var.domain_name}."
  managed_zone = google_dns_managed_zone.zone.name
  type         = each.value.type
  ttl          = 300
  rrdatas      = [each.value.rrdata]
}

resource "google_dns_record_set" "www" {
  name         = "www.${var.domain_name}."
  managed_zone = google_dns_managed_zone.zone.name
  type         = "CNAME"
  ttl          = 300
  rrdatas      = [var.domain_name]
}
